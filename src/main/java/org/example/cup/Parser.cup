package org.example.cup;

import java_cup.runtime.Symbol;
import java.util.ArrayList;
import org.example.classes.WebSite;
import org.example.classes.Page;
import org.example.classes.ClassPrincipal;
import org.example.classes.components.Component;
import org.example.classes.components.ImageComponent;
import org.example.classes.components.TextComponent;
import org.example.classes.components.VideoComponent;
import java.time.LocalDate;

class Parser;

parser code {:

    private ArrayList<WebSite> webSites = new ArrayList<>();
    private ArrayList<Page> pages = new ArrayList<>();
    private ArrayList<Component> components = new ArrayList<>();
    private LocalDate date = LocalDate.now();

    public void syntax_error(Symbol cur_token) {
            System.out.println("Simbolo con error:" + symbl_name_from_id(cur_token.sym));
            System.out.println("Linea " + cur_token.left);
            System.out.println("Columna " + cur_token.right);
            if (expected_token_ids().isEmpty()) {
                System.out.println("ya no se esperaba ningun simbolo");
            }
    }

    public ArrayList<WebSite> getWebs(){
        return this.webSites;
    }

    public ArrayList<Page> getPages(){
        return this.pages;
    }

    public ArrayList<Component> getComponents(){
        return this.components;
    }

    private String quitarCorchetes(String identificador){
        return identificador.replace("[", "").replace("]", "");
    }

    private String quitarEspacios(String space){
        return space.replace(" ", "");
    }

:}

terminal NUEVO_SITIO_WEB, ID, USUARIO_CREACION, FECHA_CREACION, FECHA_MODIFICACION, USUARIO_MODIFICACION,
BORRAR_SITIO_WEB, NUEVA_PAGINA, TITULO, TITULOCORCHETES, SITIO, PADRE, MODIFICAR_PAGINA, BORRAR_PAGINA, MODIFICAR_COMPONENTE,
PAGINA, CLASE, AGREGAR_COMPONENTE, BORRAR_COMPONENTE,  NOMBRE, IGUAL, MAYORQUE, COMILLAS,
PARRAFO, IMAGEN, VIDEO, MENU, TEXTO, ALINEACION, COLOR, ORIGEN, ALTURA, ANCHO, ETIQUETAS,  INITACCIONES, ENDACCIONES,
INITACCION, ENDACCION, INITPARAMETRO, ENDPARAMETRO, INITATRIBUT, ENDATRIBUT, INITPARAMETROS, ENDPARAMETROS,
INITATRIBUTOS, ENDATRIBUTOS, IDENTIFICADOR;

//INITETIQUETAS ENNDETIQUETAS INITETIQUETA ENDETIQUETA VALOR


//Strings
non terminal String  principalAttribut, wideAttribut, heightAttribut, originAttribut,
colorAttribut, lineUpAttribut, textAttribut, pageComponent, principal, site, title, modificationUser,
modificationDate, creationDate, userCreator, id, prePrincipal, classComponent, atributClass;

//Components
non terminal VideoComponent declaredVideoAttribut;
non terminal ImageComponent declaredImageAttribut;
non terminal TextComponent declaredTextAttribut;

non terminal Page page, newPage, modifyPage, deletePage;

non terminal WebSite siteWeb, addNewSite, deleteSite;

non terminal Component component, addComponent, modifyComponent, deleteComponent, atribut, atributExtend,
attributsDeclared;

non terminal ClassPrincipal reaction, actions, actionSingle;

non terminal onset, declaredMenuAttribut;
non terminal  initAcction, initParametro, initAttribut, labelAttribut;

start with onset;

/*
, initLabel
        Listas de tipo WebSite Page Component
*/
onset::=  actionSingle
    | actions
    ;

actions::= INITACCIONES reaction:reaction ENDACCIONES{:ClassPrincipal classPrincipal = new ClassPrincipal(webSites, pages, components); RESULT = classPrincipal;:}
        ;

reaction::= reaction actionSingle:actionSingle {:ClassPrincipal classPrincipal = new ClassPrincipal(webSites, pages, components); RESULT = classPrincipal;:}
        |
        ;
/*
  Aca se agrega a la lista
*/
actionSingle::= initAcction siteWeb:siteWeb {:ClassPrincipal classPrincipal = new ClassPrincipal();
            classPrincipal.setWebSites(webSites);
            ArrayList<WebSite> webSites;
            webSites = classPrincipal.getWebSites();
            RESULT = classPrincipal;:}
        | initAcction page:page {:ClassPrincipal classPrincipal = new ClassPrincipal();
            classPrincipal.setPages(pages); RESULT = classPrincipal;:}
        | initAcction component:component {:ClassPrincipal classPrincipal = new ClassPrincipal();
            classPrincipal.setComponents(components); RESULT = classPrincipal;:}
        | error
        ;

siteWeb::= addNewSite:addNewSite {:webSites.add(addNewSite);
                                RESULT = addNewSite;:}
        | deleteSite:deleteSite {:webSites.add(deleteSite); RESULT = deleteSite;:}
        ;

//MENORQUE ACCION NOMBRE IGUAL COMILLAS
page::= newPage:newPage {:pages.add(newPage); RESULT = newPage;:}
    | modifyPage:modifyPage {:pages.add(modifyPage); RESULT = modifyPage;:}
    | deletePage:deletePage {:pages.add(deletePage); RESULT = deletePage;:}
    ;

component::= addComponent:addComponent {:components.add(addComponent); RESULT = addComponent;:}
        | modifyComponent:modifyComponent {:components.add(modifyComponent); RESULT = modifyComponent;:}
        | deleteComponent:deleteComponent {:components.add(deleteComponent); RESULT = deleteComponent;:}
        ;

addNewSite::= NUEVO_SITIO_WEB COMILLAS MAYORQUE
            INITPARAMETROS
            id:id
            userCreator:userCreator
            creationDate:creationDate
            modificationDate:modificationDate
            modificationUser:modificationUser
            ENDPARAMETROS
            ENDACCION
            {:WebSite webSite = new WebSite(id, userCreator.toString(), creationDate.toString(), modificationDate.toString(), modificationUser.toString(), false);
            RESULT = webSite;
            :}
           |NUEVO_SITIO_WEB COMILLAS MAYORQUE
                        INITPARAMETROS
                        id:id
                        ENDPARAMETROS
                        ENDACCION
                        {:WebSite webSite = new WebSite(id, "UserDefault", date.toString(), date.toString(),
                        "UserDefault", false);:}
           ;


deleteSite::= BORRAR_SITIO_WEB COMILLAS MAYORQUE
            INITPARAMETROS
            id:id
            ENDPARAMETROS
            ENDACCION
            {:WebSite webSite = new WebSite(id, true); RESULT = webSite;:}
            ;

newPage::=  NUEVA_PAGINA COMILLAS MAYORQUE
            INITPARAMETROS
            id:id
            title:title
            site:site
            prePrincipal:prePrincipal
            userCreator:userCreator
            creationDate:creationDate
            modificationDate:modificationDate
            modificationUser:modificationUser
            ENDPARAMETROS
            ENDACCION
            {:Page page = new Page(id, title, site, prePrincipal, userCreator, creationDate, modificationDate, modificationUser, 1); RESULT = page;:}
            | NUEVA_PAGINA COMILLAS MAYORQUE
                          INITPARAMETROS
                          id:id
                          site:site
                          prePrincipal:prePrincipal
                          ENDPARAMETROS
                          ENDACCION
                          {:Page page = new Page(id, "", site, prePrincipal, "UsuarioFinal", date.toString(), date.toString(), "UsuarioDefault", 1); RESULT = page;:}
                          ;

deletePage::=  BORRAR_PAGINA COMILLAS MAYORQUE
               INITPARAMETROS
               id:id
               ENDPARAMETROS
               ENDACCION
               {:Page page = new Page(id, 3); RESULT = page;:}
               ;

modifyPage::= MODIFICAR_PAGINA COMILLAS MAYORQUE
              INITPARAMETROS
              id:id
              title:title
              ENDPARAMETROS
              ENDACCION
              {:Page page = new Page(id, title, 2); RESULT = page;:}
              ;

addComponent::= AGREGAR_COMPONENTE COMILLAS MAYORQUE
                INITPARAMETROS
                id:id
                pageComponent:pageComponent
                classComponent:classComponent
                ENDPARAMETROS
                INITATRIBUTOS
                atribut:atribut
                ENDATRIBUTOS
                ENDACCION
                {:
                    atribut.setIdComponent(id);
                    atribut.setPage(pageComponent);
                    atribut.setClassComponent(classComponent);
                    RESULT = atribut;
                 :}

                ;

deleteComponent::= BORRAR_COMPONENTE COMILLAS MAYORQUE
                INITPARAMETROS
                id:id
                pageComponent:pageComponent
                ENDPARAMETROS
                ENDACCION
                {:System.out.println("Se hizo delete de componente");Component component = new Component(id, pageComponent, " ", 4); RESULT = component;:}
                ;

modifyComponent::= MODIFICAR_COMPONENTE COMILLAS MAYORQUE
                INITPARAMETROS
                id:id
                pageComponent:pageComponent
                classComponent:classComponent
                ENDPARAMETROS
                INITATRIBUTOS
                atribut:atribut
                ENDATRIBUTOS
                ENDACCION
                {:
                    atribut.setIdComponent(id);
                    atribut.setPage(pageComponent);
                    atribut.setClassComponent(classComponent);
                    atribut.setFlag(7);
                    RESULT = atribut;
                :}
                ;

id::= initParametro ID COMILLAS MAYORQUE
        IDENTIFICADOR:id
        ENDPARAMETRO
        {:RESULT = quitarCorchetes(id.toString());:}
        ;

userCreator::= initParametro USUARIO_CREACION COMILLAS MAYORQUE
            IDENTIFICADOR:userCreator
            ENDPARAMETRO
            {:
            String conditional = quitarCorchetes(quitarEspacios(userCreator.toString()));
            if(conditional.equals("") ){
                userCreator = "UsuarioDefault";
            }RESULT = quitarCorchetes(userCreator.toString());:}
            ;

creationDate::= initParametro FECHA_CREACION COMILLAS MAYORQUE
                IDENTIFICADOR:creationDate
                ENDPARAMETRO
                {:
                String conditional = quitarCorchetes(quitarEspacios(creationDate.toString()));
                if(conditional.equals("")){
                    creationDate = ""+date;
                }
                RESULT = quitarCorchetes(creationDate.toString());:}
                ;

modificationDate::= initParametro FECHA_MODIFICACION COMILLAS MAYORQUE
                IDENTIFICADOR:modificationDate
                ENDPARAMETRO
                {:
                String conditional = quitarCorchetes(quitarEspacios(modificationDate.toString()));
                    if(conditional.equals("")){
                        modificationDate = ""+date;
                    }
                RESULT = quitarCorchetes(modificationDate.toString());:}
                ;

modificationUser::= initParametro USUARIO_MODIFICACION COMILLAS MAYORQUE
                IDENTIFICADOR:modificationUser
                ENDPARAMETRO
                {:
                String conditional = quitarCorchetes(quitarEspacios(modificationUser.toString()));
                if(conditional.equals("")){
                modificationUser = "UsuarioDefault";
                }
                RESULT = quitarCorchetes(modificationUser.toString());:}
                ;

title::= initParametro TITULO COMILLAS MAYORQUE
        IDENTIFICADOR:title
        ENDPARAMETRO
        {:RESULT = quitarCorchetes(title.toString());:}
        ;

site::= initParametro SITIO COMILLAS MAYORQUE
        IDENTIFICADOR:site
        ENDPARAMETRO
        {:RESULT = quitarCorchetes(site.toString());:}
        ;

prePrincipal::= principal:principal
            {:RESULT = principal;:}
            ;

principal::= initParametro PADRE COMILLAS MAYORQUE
            IDENTIFICADOR: principal
            ENDPARAMETRO
            {:RESULT = quitarCorchetes(principal.toString());:}
            ;

pageComponent::= initParametro PAGINA COMILLAS MAYORQUE
                IDENTIFICADOR:pageComponent
                ENDPARAMETRO
                {:RESULT = quitarCorchetes(pageComponent.toString());:}
                ;

classComponent::= initParametro CLASE COMILLAS MAYORQUE
                atributClass:atributClass
                ENDPARAMETRO
                {:RESULT = atributClass;:}
                ;

atribut::= atributExtend:atributs{:RESULT = atributs;:}
        ;



atributExtend::= initAttribut attributsDeclared:attrDcl
                  {:RESULT = attrDcl;:}
                ;

attributsDeclared::= declaredTextAttribut:decltxt {:RESULT = decltxt;:}
                   | declaredImageAttribut:declImg {:RESULT = declImg;:}
                   | declaredVideoAttribut:declVide {:RESULT = declVide;:}
                   | declaredMenuAttribut
                    ;

declaredTextAttribut::= textAttribut:txtAt initAttribut lineUpAttribut:lineAt initAttribut colorAttribut:colorAt
                    {:TextComponent textComponent = new TextComponent(txtAt, lineAt, colorAt, 1);
                    RESULT = textComponent;:}
                    | textAttribut:txt initAttribut lineUpAttribut:lineAt {:TextComponent textComponent = new TextComponent(txt, lineAt, 1); RESULT = textComponent; :}
                    | textAttribut:txt initAttribut colorAttribut:color {:TextComponent textComponent = new TextComponent(txt, 1, color); RESULT = textComponent;:}
                    | textAttribut:txt {:TextComponent textComponent = new TextComponent(txt, 1); RESULT = textComponent;:}
                    ;


declaredImageAttribut::= originAttribut:originAt initAttribut lineUpAttribut:lineAt initAttribut heightAttribut:heightAt  initAttribut wideAttribut:wideAt
                        {:ImageComponent imageComponent = new ImageComponent(originAt, lineAt, heightAt, wideAt, 2);
                        RESULT = imageComponent;:}
//                        | originAttribut:originAt initAttribut heightAttribut:heightAt initAttribut wideAttribut:wideAt
//                        {:ImageComponent imageComponent = new ImageComponent(originAt, heightAt, wideAt, 2); RESULT imageComponent;:}
                        ;

declaredVideoAttribut::= originAttribut:originAt initAttribut heightAttribut:heightAt initAttribut wideAttribut:wideAt
                        {:VideoComponent videoComponent = new VideoComponent(originAt, heightAt, wideAt, 3);
                        RESULT = videoComponent;:}
                    ;

declaredMenuAttribut::= principalAttribut labelAttribut
                    ;
atributClass::= TITULOCORCHETES:titulo {:RESULT = quitarCorchetes(titulo.toString());:}
                | PARRAFO:parrafo {:RESULT = quitarCorchetes(parrafo.toString());:}
                | IMAGEN:imagen {:RESULT = quitarCorchetes(imagen.toString());:}
                | VIDEO:video {:RESULT = quitarCorchetes(video.toString());:}
                | MENU:menu {:RESULT = quitarCorchetes(menu.toString());:}
                ;

textAttribut::= TEXTO COMILLAS MAYORQUE
               IDENTIFICADOR:textAttribut
               ENDATRIBUT
               {:RESULT = quitarCorchetes(textAttribut.toString());:}
               ;

lineUpAttribut::=  ALINEACION COMILLAS MAYORQUE
                  IDENTIFICADOR:lineUpAttribut
                  ENDATRIBUT
                  {:RESULT = quitarCorchetes(lineUpAttribut.toString());:}
                  ;

colorAttribut::=  COLOR COMILLAS MAYORQUE
                 IDENTIFICADOR:hexadecimal
                 ENDATRIBUT
                 {:RESULT = quitarCorchetes(hexadecimal.toString());:}
                 ;

originAttribut::=  ORIGEN COMILLAS MAYORQUE
                 IDENTIFICADOR:originAttribut
                 ENDATRIBUT
                 {:RESULT = quitarCorchetes(originAttribut.toString());:}
                 ;

heightAttribut::=  ALTURA COMILLAS MAYORQUE
                 IDENTIFICADOR:heightAttribut
                 ENDATRIBUT
                 {:RESULT = quitarCorchetes(heightAttribut.toString());:}
                 ;

wideAttribut::=  ANCHO COMILLAS MAYORQUE
                IDENTIFICADOR:wideAttribut
                ENDATRIBUT
                {:RESULT = quitarCorchetes(wideAttribut.toString());:}
                ;

principalAttribut::=  PADRE COMILLAS MAYORQUE
                IDENTIFICADOR:principalAttribut
                ENDATRIBUT
                {:RESULT = quitarCorchetes(principalAttribut.toString());:}
                ;

labelAttribut::= ETIQUETAS COMILLAS MAYORQUE
                IDENTIFICADOR
                ENDATRIBUT
                ;


initAcction::= INITACCION NOMBRE IGUAL COMILLAS
            ;

initParametro::= INITPARAMETRO NOMBRE IGUAL COMILLAS
            ;

initAttribut::= INITATRIBUT NOMBRE IGUAL COMILLAS
            ;
/*
initLabel::= INITETIQUETA VALOR IGUAL COMILLAS
*/